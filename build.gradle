plugins {
  id 'groovy'
  id 'codenarc'
  id 'java-gradle-plugin'
  id 'com.gradle.plugin-publish' version '1.1.0'
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

wrapper {
  gradleVersion = '8.0'
}

repositories {
  mavenCentral()
}

dependencies {
  implementation gradleApi()
  implementation "org.xmlresolver:xmlresolver:${xmlresolverVersion}"
  implementation 'org.relaxng:jing:20220510'
  implementation 'org.relaxng:trang:20220510'
}

test {
  testLogging {
    showStandardStreams = true
    exceptionFormat = 'full'
  }
}

codenarc {
    toolVersion = '3.2.0'
    configFile = file('config/codenarc/codenarc.groovy')
}

task setupStage(dependsOn: ["assemble", "test"]) {
  doLast {
    mkdir "${buildDir}/stage"
  }

  doLast {
    copy {
      from projectDir
      into "${buildDir}/stage"
      include "README.md"
      include "LICENSE"
    }
  }

  doLast {
    copy {
      from "${buildDir}/libs"
      into "${buildDir}/stage"
    }
  }
}

task dist(dependsOn: ["setupStage"], type: Zip) {
  from("${buildDir}/stage")
  into "relaxng-gradle-${project.version}"
  archiveFileName = "relaxng-gradle-${project.version}.zip"
}

gradlePlugin {
  website = 'https://github.com/ndw/relaxng-gradle'
  vcsUrl = 'https://github.com/ndw/relaxng-gradle.git'
  plugins {
    RelaxNGValidatePlugin {
      id = 'com.nwalsh.gradle.relaxng.validate'
      displayName = 'RELAX NG validation plugin'
      implementationClass = 'com.nwalsh.gradle.relaxng.validate.RelaxNGValidatePlugin'
      description = 'A Gradle plugin for doing RELAX NG validation'
      tags.set(['relaxng', 'schema', 'validation', 'jing', 'xml'])
    }
    RelaxNGTranslatePlugin {
      id = 'com.nwalsh.gradle.relaxng.translate'
      displayName = 'RELAX NG translation plugin'
      implementationClass = 'com.nwalsh.gradle.relaxng.translate.RelaxNGTranslatePlugin'
      description = 'A Gradle plugin for doing RELAX NG translation'
      tags.set(['relaxng', 'schema', 'transformation', 'trang', 'xml'])
    }
  }
}
